	NAM	PLAY68		REV.B MAY 1978
	OPT	O,NOP
* COPYRIGHT (C) 1978 ALL RIGHTS RESERVED.
* NEWTECH COMPUTER SYSTEMS,INC.
*
* PLAY68 STARTS AT THE BEGINNING OF THE MEMORY AREA
* DESIGNATED "SCORE" AND TRANSFERS INTO RAM LOCATION
* "PITCH" A 1-BYTE PITCH PARAMETER AND INTO RAM
* LOCATION "DURA" A 2-BYTE DURATION PARAMETER.
* THE ROUTINE THEN OUTPUTS TO
* THE MODEL 68 THE MUSICAL NOTE SPECIFIED BY THESE
* NOTE PARAMETERS.  PLAY68 CONTINUES TRANSFERRING
* NOTE PARAMETERS AND OUTPUTING EACH NOTE UNTIL
* A PITCH CONSTANT OF ZERO IS ENCOUNTERED WHICH
* INDICATES THE END OF THE MUSICAL SCORE.
* THIS ROUTINE DOES NOT USE THE STACK.
*
BASEND	EQU	$1EE2		;END OF SWTPC 8K BASIC 2.2
	ORG	BASEND+$20
SCORE	EQU	BASEND+$0120	;SCORE LOCATION
	LDX	#SCORE		;INIT. SCORE POINTER.
	STX	PLACE
	BRA	NEXT
EXIT1	RTS
	NOP
	NOP
NEXT	LDX	PLACE
	LDAA	#0
	CMPA	0,X
HERE	BEQ	EXIT1		;YOUR ENDING?
*   ELSE TRANSFER PARAMTERS FOR NEXT NOTE OR SCORE
*   INTO PLAY ROUTINE
	LDAA	0,X	 	;LOAD PITCH
	STAA	PITCH
	INX
	LDAA	0,X		;LOAD DURATION MSB
	ANDA	#%00000111	;MASK 3 LSB'S
	STAA	DURA
	LDAA	0,X		;GET MSB AGAIN
	ANDA	#%01110000	;MASK 3 BITS
	ADDA	#TBL1		;ENVELOPE SPEC ADDRESS
	STAA	PLAY+2		;LOAD ENVELOPE POINTER
	INX
	LDAA	0,X		;LOAD DURATION LSB.
	STAA	DURA+1
	INX
	STX	PLACE		;SAVE SCORE POINTER
*    THE PLAY ROUTING PLAYS ONE NOTE
PLAY	LDX	#TBL1		;INIT EVELOPE POINTER.
	STX	TBL1P		;STORE ENVELOPE POINTER.
	LDAB	0,X		;PUT AMPLITUDE VALUE IN B.
	LDX	DURA		;LOAD DURATION PARAMETER
*				 INTO INDEX REGISTER.
LOOP3	CPX	$E000		;5-WASTE TIME (31 STATES)
	CPX	$E000		;5-
	CPX	$E000		;5-
	CPX	$E000		;5-
	CPX	$E000		;5-
	COM	$E000		;6-
LOOP2	LDAA	#22		;4-FIXED DELAY TO ADJUST
LOOP4	DECA			;2- LOWEST NOTE TO 262HZ
	BNE	LOOP4		;4- (MIDDLE c) WHEN PITCH
*				   PARAMETER=FE
	LDAA	PITCH		;4-LOAD PITCH PARAMETER
	STAB	MOD68		;5-OUTPUT TO MUSIC BOARD
LOOP1	DECA			;2-DELAY AS PER PITCH PARAM.
	BNE	LOOP1		;4-
	COMB			;2-COMPLEMENT WAVEFORM VALUE.
	DEX			;4-DECREMENT DURATION COUNTER.
	BNE	LOOP3		;4-
	INC	TBL1P+1		;6-SET UP NEXT SEGMENT
	LDX	TBL1P		;5-
	LDAB	0,X		;5-
	CMPB	#$01		;2-END OF ENVELOPE CHAR.=01
	BEQ	NEXT		;4-GO DO NEXT NOTE.
	LDX	DURA		;5-RESET DURATION PARAMETER
	BRA	LOOP2		;4-
*
* AMPLITUDE ENVELOPE SPECIFICATION:
* MAXIMUM APLITUDE IS OUTPUT WHEN ACCUMULATOR B IS
* COMPLEMENTED FROM 00 TO FF AND BACK.  MINIMUM
* AMPLITUDE IS OUTPUT WHEN B IS COMPLEMENTED
* BETWEEN 80 AND 7F.  AN END OF ENVELOPE RECORD
* OF $01 MARKS THE END OF THE SPECIFICATION.
*
TBL1	EQU	*		ENVELOPE SPECIFICATIONS
* TABLE 1 - ATTACK, #5
	FCB	$FF,$FF,$F8,$F0,$E8,$E0,$D8,$D0
	FCB	$C8,$C0,$B8,$B0,$A0,$90,$85,$01
* TABLE 2 - REST, #R
	FCB	$80,$80,$80,$80,$80,$80,$80,$80
	FCB	$80,$80,$80,$80,$80,$80,$80,$01
* TABLE 3 - STACCATO, #S
	FCB	$E0,$F0,$FF,$E5,$C8,$BD,$B0,$A5
	FCB	$98,$8D,$80,$80,$80,$80,$80,$01
* TABLE 4 - LEGATO, #L
	FCB	$E0,$F0,$FF,$FF,$FF,$FF,$FF,$FF
	FCB	$FF,$FF,$FF,$E8,$D0,$CD,$C8,$01
* TABLE 5 - SOFT STACCATO, #1
	FCB	$D8,$BD,$C0,$B8,$B0,$A0,$90,$88
	FCB	$80,$80,$80,$80,$80,$80,$80,$01
* TABLE 6 - SOFT LEGATO, #2
	FCB	$B8,$BD,$C0,$C0,$C0,$C0,$C0,$C0,
	FCB	$C0,$C0,$C0,$B8,$B0,$A8,$A0,$01
* TABLE 7 - "SHAPED", #3
	FCB	$D0,$D6,$DD,$E3,$E8,$F5,$FF,$FF,
	FCB	$FF,$FF,$FF,$F3,$E5,$DA,$D0,$01
* TABLE 8 - CRESCENDO, #4
	FCB	$B8,$BE,$C4,$CA,$D0,$D6,$DC,$E2,
	FCB	$E8,$F4,$FF,$FF,$FF,$CD,$A0,$01
*
DURA	RMB	2		;DURATION CONSTANT
MOD68	EQU	$8010		;MUSIC BOARD IN I/0 SLOT 4.
PLACE	RMB	2
TBL1P	RMB	2		;TABLE POINTER.
PITCH	RMB	1		;PITCH PARAMETER.

	ORG	BASEND
* KEYTONE ROUTINE
* THE FOLLOWING ROUTINE PROVIDES A SHORT TONE
* OR "BEEP" EACH TIME A KEY IS STRUCK ON THE
* KEYBOARD AT PORT 1.  THIS IS THE PORT THAT
* BASIC NORMALLY USES AS ITS CONTROL PORT.
BEEP	FCB	$20,$28,$08,$00	;"BEEP" SCORE
INEEE	EQU	$E1AC		;MIKBUG/SWTBUG INPUT
PRMFLG	EQU	$85		;BASIC PROMPT FLAG
KEYTON	JSR	INEEE		;GET INPUT CHARACTER
	PSHA			;SAVE CHARACTER
	LDAA	PRMFLG		;PROMPT REQUIRED?
	BNE	ENDTON		;GO IF NOT
	LDX	#BEEP		;POINT TO SCORE
	STX	PLACE		
	JSR	NEXT		;PLAY BEEP TONE
ENDTON	PULA			;RESTORE CHARACTER
	RTS

* FORCE PORT 1 INPUT TO GO TO KEYTON ROUTINE
	ORG	$112		;PORT 1 INPUT JUMP
	JMP	KEYTON		;INPUT VECTOR

* FORCE BASIC TO LEAVE PLAY68 MEMORY UNUSED
	ORG	$14E		;BASIC WORK SPACE POINTER
	FDB	BASEND+$400	;RESERVE 1K

* PLUG MIKBIG/SWTBUG PROGRAM COUNTER TO CAUSE
* ENTRY INTO SWTPC 8K BASIC 2.2.
	ORG 	$A048		;PROGRAM COUNTER
	FDB	$100		;BASIC "COLD ENTRY POINT
	END